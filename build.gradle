plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.3-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '20c7ec554a'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.3.0' apply false
}

preprocess {
	// def mc114      = createNode('1.14.4'  , 1_14_04, 'yarn')
	// def mc115      = createNode('1.15.2'  , 1_15_02, 'yarn')
	// def mc116      = createNode('1.16.5'  , 1_16_05, 'yarn')
	def mc117      = createNode('1.17.1'  , 1_17_01, 'yarn')
	def mc118      = createNode('1.18.2'  , 1_18_02, 'yarn')
	def mc119      = createNode('1.19.4'  , 1_19_04, 'yarn')
	def mc120      = createNode('1.20.1'  , 1_20_01, 'yarn')

	mc120.link(mc119, file('versions/mapping-1.20.1-1.19.4.txt'))
	mc119.link(mc118, file('versions/mapping-1.19.4-1.18.2.txt'))
	mc118.link(mc117, file('versions/mapping-1.18.2-1.17.1.txt'))
	// mc117.link(mc116, file('versions/mapping-1.17.1-1.16.5.txt'))
	// mc116.link(mc115, file('versions/mapping-1.16.5-1.15.2.txt'))
	// mc115.link(mc114, file('versions/mapping-1.15.2-1.14.4.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
